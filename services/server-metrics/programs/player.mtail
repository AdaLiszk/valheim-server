
#
# Typical Lifecycle of a Player:
#
# 1. Connect: `Got handshake from client 76561198108936133`
# 2. Version Check: `VERSION check their:0.146.11 mine:0.146.11`
# 3. Spawn: `Got character ZDOID from Testvazz : 954855457:113`
# 4. Died: `Got character ZDOID from Testvazz : 0:0`
# 5. Disconnect: `Closing socket 76561198108936133`
#

const VERSION /(?P<version>\d+\.\d+\.\d+)/

const STEAM_ID /(?P<steam_id>\d{17})/
const PLAYER_ID /(?P<player_id>\d{9})/
const PLAYER_NICK /(?P<player_nick>.*)/

gauge server_game_version by version

counter player_connected_count by steam_id
gauge player_connected_last by steam_id
hidden gauge steam_id

gauge player_zdoid_by_steamid by steam_id
gauge player_nick_by_steamid by steam_id
gauge player_steamid_by_zdoid by player_id
gauge player_steamid_by_nick by player_nick

gauge player_character by steam_id, player_id, player_nick
gauge player_game_version by steam_id, version

counter player_died by player_nick, steam_id

counter player_disconnected_count by steam_id
gauge player_disconnected_last by steam_id

gauge server_random_event_possible
counter player_found_location by location
counter server_random_event by name


# CONNECTIONS
# =================================================================================================

# Got status changed msg k_ESteamNetworkingConnectionState_Connecting
# Got status changed msg k_ESteamNetworkingConnectionState_Connected
# Got connection SteamID 76561198108936133
# Got handshake from client 76561198108936133
# VERSION check their:0.147.3 mine:0.147.3
# Peer 76561198108936133 has wrong password
#

/Got handshake from client / + STEAM_ID {
    player_connected_count[$steam_id]++
    player_connected_last[$steam_id] = timestamp()

    steam_id = $steam_id
}

/VERSION check their:/ + VERSION {
    player_game_version[steam_id][$version] = $version

    /mine:/ + VERSION {
        server_game_version[$version] = $version
    }
}

/Closing socket / + STEAM_ID {
    player_disconnected_count[$steam_id]++
    player_disconnected_last[$steam_id] = timestamp()
}

# IN-GAME ACTIONS
# =================================================================================================

#
# Actions that the players can do
#
# Got character ZDOID from Testvazz : 0:0
# Found location of type Eikthyrnir
# Possible events:0
# Possible events:1
# Random event set:army_theelder
#

/Got character ZDOID from / + PLAYER_NICK + / : / + PLAYER_ID + /:\d+/ {
    player_steamid_by_nick[$player_nick] < 1 {
        player_steamid_by_nick[$player_nick] = steam_id
    }
    otherwise {
        steam_id = player_steamid_by_nick[$player_nick]
    }

    player_steamid_by_zdoid[$player_id] = steam_id
    player_zdoid_by_steamid[steam_id] = $player_id
    player_nick_by_steamid[steam_id] = $player_nick

    player_character[steam_id, $player_id, $player_nick] = $player_id
}

/Got character ZDOID from / + PLAYER_NICK + / : 0:0/ {
    player_steamid_by_nick[$player_nick] > 0 {
        steam_id = player_steamid_by_nick[$player_nick]
    }
    otherwise {
        steam_id = "UNKNOWN"
    }

    player_died[steam_id][$player_nick]++
}

/Found location of type (?P<location>\w+)/ {
    player_found_location[$location]++
}

/Possible events:(?P<events>\d+)/ {
    server_random_event_possible = $events
}

/Random event set:(?P<event>\w+)/ {
    server_random_event[$event]++
}